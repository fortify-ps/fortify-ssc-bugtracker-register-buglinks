buildscript {
    configurations.all {
        resolutionStrategy.dependencySubstitution {
            substitute module("com.overzealous:remark:1.1.0") using module("com.wavefront:remark:2023-07.07") because "not available on maven central anymore"
        }
    }
}

plugins {
  id 'io.freefair.lombok' version '8.13.1'
  id 'com.github.jk1.dependency-license-report' version '2.9'
  id 'org.kordamp.gradle.markdown' version '2.2.0'
  id 'biz.aQute.bnd.builder' version '7.1.0'
}

group 'com.fortify.ssc.bugtracker.register-buglinks'
ext.getVersion = {
	def result = project.findProperty('version');
	return !result || result=='unspecified' ? new Date().format('0.yyyyMMdd.HHmmss') : result;
}
version = ext.getVersion();

ext {
	gradleHelpersLocation = "https://raw.githubusercontent.com/fortify/shared-gradle-helpers/1.8"
}

apply from: "${gradleHelpersLocation}/repo-helper.gradle"
apply from: "${gradleHelpersLocation}/junit-helper.gradle"
apply from: "${gradleHelpersLocation}/thirdparty-helper.gradle"
apply from: "${gradleHelpersLocation}/readme2html.gradle"

//apply plugin: 'java'
sourceCompatibility = 17

dependencies {
    compileOnly fileTree(dir: 'lib')
}

jar {
  bundle { 
    bnd (
    'Bundle-Name': 'Register Bug Links',
    'Bundle-Vendor': 'Micro Focus Fortify',
	'Bundle-ActivationPolicy': 'lazy',
    'Bundle-Localization': 'plugin',
    'Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8',
    'Built-By': 'Fortify',
    'Version': project.version,
	'Bundle-SymbolicName': 'com.fortify.BugTrackerPluginRegisterBugLinks;singleton:=true',
    'Bundle-Version': project.version,
    'Require-Bundle': 'com.fortify.common.public-api',    // Needed by AWB
    'Import-Package': '!*',   // ..suppress the bndTools default import generation
    'Export-Package': 'com.fortify.bugtracker.register_buglinks'
    )
  } 
}

task dist(type: Zip) {
	dependsOn 'build', 'readme2html'
	archiveFileName = "${rootProject.name}-${project.version}.zip"
	destinationDirectory = file("$buildDir/dist")
	from("${buildDir}/${libsDirName}") {
		include "${rootProject.name}-${project.version}.jar"
	}
	from "${buildDir}/html"
	from("${projectDir}") {
		include "LICENSE.TXT"
	}
}
